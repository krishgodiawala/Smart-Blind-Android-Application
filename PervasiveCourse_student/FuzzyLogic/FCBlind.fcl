/*
	Fuzzy logic controller example for motor control
*/

FUNCTION_BLOCK motor	// Block definition (there may be more than one block per file)

VAR_INPUT				// Define input variables
	temperature : REAL;
	ambient : REAL;
END_VAR

VAR_OUTPUT				// Define output variable
	blind : REAL;
END_VAR

// Fuzzify input variable 'temperature': {'freezing', 'cold', 'comfort', 'warm', 'hot'}
// value 1 to 100
FUZZIFY temperature			
	TERM freezing := (0, 1) (25, 0); 
	TERM cold := (0, 0) (25, 1) (50, 0);
	TERM comfort := (25, 0) (50, 1) (75, 0);
	TERM warm := (50, 0) (75, 1) (100, 0);
	TERM hot := (75, 0) (100, 1);
END_FUZZIFY

// Fuzzify input variable 'ambient': { 'dark', 'dim', 'bright' }
// value from 1 to 100
FUZZIFY ambient			
	TERM dark := (1, 1) (25, 1) (35,0) ;
	TERM dim := (25, 0) (50, 1) (75, 0);
	TERM bright := (65, 0) (75, 1) (100, 1);
END_FUZZIFY

// Defzzzify output variable 'blind' : {'open', 'half', 'close' }
// value from 1 to 100
DEFUZZIFY blind			
	TERM open := (1, 1) (25, 1) (50, 0);
	TERM half := (25, 0) (50, 1) (75, 0);
	TERM close := (50, 0) (75,1) (100,1);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)
	ACT : MIN;			// Use 'min' activation method
	ACCU : MAX;			// Use 'max' accumulation method

	RULE 1 : IF temperature IS hot AND ambient IS dim THEN blind IS close;
	RULE 2 : IF temperature IS cold AND ambient IS bright THEN blind IS half;
	RULE 3 : IF temperature IS warm AND ambient IS dim THEN blind IS half;
	RULE 4 : IF temperature IS warm AND ambient IS bright THEN blind IS close;
	RULE 5 : IF ambient IS dark THEN blind IS open;
END_RULEBLOCK

END_FUNCTION_BLOCK

